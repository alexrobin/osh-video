apply from: gradle.oshCoreDir + '/common.gradle'
apply from: gradle.oshCoreDir + '/release.gradle'
version = oshCoreVersion


subprojects {  
  // inject all repositories from included builds if any
  repositories.addAll(rootProject.repositories)
  
  // add project dependency to proper configuration depending on version
  // this is used to build different zip distributions
  afterEvaluate { p ->
    rootProject.dependencies {
      def dep = p.version.endsWith('SNAPSHOT') ? dev(p) : stable(p);
      dep.exclude group: 'org.sensorhub'
    }
  }
}


// create one configuration for each distribution
configurations {
  stable
  dev
}

  
// distributions content
distributions {
  dev {
    baseName = 'osh-video-dev'
    contents {
      into('lib') {
        from project.configurations.dev
      } 
    }
  }

  stable {
    baseName = 'osh-video-stable'
    contents {
      into('lib') {
        from project.configurations.stable
      } 
    }
  }
}
